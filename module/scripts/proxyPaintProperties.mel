/*
 *  proxyPaintProperties.mel
 *
 */ 

global proc proxyPaintProperties ()
{
	setUITemplate -pushTemplate DefaultTemplate;

	string $parent = `toolPropertyWindow -q -location`;
    setParent $parent;
	string $curctx = `currentCtx`;

	string $ppnForm = `formLayout proxyPaint`;
	string $ppnColumn = `columnLayout  -adjustableColumn true proxyPaintOptions`;

/// begin of proxyPaintOptions
	text -ann "2" proxyPaint_vizNameText;
	
	paneLayout -h 400 -configuration "horizontal2";
/// begin of pane
	scrollLayout;
/// begin of scroll up
        	
    string $vizColn = `columnLayout -adjustableColumn true pxpnVizUIColn34952260`;
	setParent ..;
	
/// end of scroll up
	setParent ..;
	
	string $viz = proxyPaint_getCurrentViz();
    int $nodeVer = proxyPaint_getNodeVersion($viz);
	   
	scrollLayout;
/// begin of scroll down

        string $exmpColn = `columnLayout -adjustableColumn true pxpnExmpUIColn1cc22e1b`;
            setParent ..;
/// end of scroll down
            setParent ..;
            
/// end of pane
    setParent ..;

/// end of proxyPaintOptions	
	setParent ..;
	
	string $ppnTab = `tabLayout `;
              
	formLayout -edit
     
        -attachControl     $ppnTab "top"    2 $ppnColumn
        -attachForm    $ppnTab "left"   2
        -attachForm    $ppnTab "bottom" 2
        -attachForm     $ppnTab "right" 2
        
        -attachForm $ppnColumn "top" 2   
         -attachForm $ppnColumn "left"   2
         -attachNone $ppnColumn "bottom"
         -attachForm $ppnColumn "right" 2 
        
     $ppnForm;
     
        string $vizColn = `columnLayout -adjustableColumn true pxpnVizColn`;
        setParent ..;
        
        string $groundColn = `columnLayout -adjustableColumn true pxpnGroundColn`;
        setParent ..; 
        
        string $edtColn = `columnLayout -adjustableColumn true pxpnEditColn`;
        setParent ..; 
	
        string $injectColn = `columnLayout -adjustableColumn true pxpnInjectColn`;                    
        setParent ..;  
        
    tabLayout -edit
        -tabLabel $vizColn "Viz"
        -tabLabel $groundColn "Ground"
        -tabLabel $edtColn "Paint"
         -tabLabel $injectColn "Misc" 
         $ppnTab;
         
	setUITemplate -popTemplate;

/// setup tab content	
	proxyPaintCreateVizUI($vizColn); 
    proxyPaintCreateGroundUI($groundColn);
    proxyPaintCreateEditUI($edtColn);
	proxyPaintCreateInjectUI($injectColn);

	proxyPaintCreateExmpUI("pxpnExmpUIColn1cc22e1b");
	proxyPaintUpdateExampleScroll();
}

global proc proxyPaint_listAllVizToPaint()
{
    string $chds[] = `columnLayout -q - ca "pxpnVizUIColn34952260"`;
    string $achd;
    for($achd in $chds) {
        deleteUI -control $achd;
    }
    string $vizs[] = `ls -type proxyViz`;
    int $nvizs = size($vizs);
    int $i;
    for($i = 0;$i<$nvizs;$i++) {
        button -l $vizs[$i] 
            -al left -w 320
            -p "pxpnVizUIColn34952260"
            -c ("proxyPaint_selectAViz2("+ $i +")");
    }
}

global proc proxyPaint_selectAViz2(int $index)
{
    string $vizs[] = `ls -type proxyViz`;
    
    select -r $vizs[$index];
    proxyPaintUpdateExampleScroll;
    
    int $nvizs = size($vizs);
    int $i;
    for($i = 0;$i<$nvizs;$i++) {
        if($i == $index) {
            setAttr ($vizs[$i] + ".act") 1;
        }
        else {
            setAttr ($vizs[$i] + ".act") 0;
        }
    }
    
    string $toolName = `currentCtx`;
    
    proxyPaintToolContext -e -slv $toolName;
    
    proxyPaint_updateVizStats($toolName);
    
}

global proc proxyPaintUpdateExampleScroll()
{
    proxyPaintToolContext -e -plantType 0 `currentCtx`;
    string $node = proxyPaint_getCurrentViz();
    if($node == "") {
        text -e -label "No Viz Selected" proxyPaint_vizNameText;
        return;
    }
    
    text -e -label $node proxyPaint_vizNameText;
    proxyPaintUpdateExmpUI("pxpnExmpUIColn1cc22e1b");
    
}
//:~
