
source "proxyPaintUIWidgets.mel";

global proc proxyPaintProperties ()
{
	setUITemplate -pushTemplate DefaultTemplate;

	string $parent = `toolPropertyWindow -q -location`;
    setParent $parent;
	string $curctx = `currentCtx`;

	string $ppnForm = `formLayout proxyPaint`;
	string $ppnColumn = `columnLayout  -adjustableColumn true proxyPaintOptions`;
// begin of proxyPaintOptions
    text -ann "2" proxyPaint_vizNameText;	
		
	textScrollList -ams off 
		-sc "proxyPaint_selectVizToPaint" 
		proxyPaint_VizToPaintScroll;
	
	string $viz = proxyPaint_getCurrentViz();
    int $nodeVer = proxyPaint_getNodeVersion($viz);
	   
	    scrollLayout -h 120;
        string $exmpColn = `columnLayout -adjustableColumn true pxpnExmpUIColn1cc22e1b`;
            setParent ..;
            setParent ..;

// end of proxyPaintOptions	
	setParent ..;
	
	string $ppnTab = `tabLayout `;
              
	formLayout -edit
     
        -attachControl     $ppnTab "top"    2 $ppnColumn
        -attachForm    $ppnTab "left"   2
        -attachForm    $ppnTab "bottom" 2
        -attachForm     $ppnTab "right" 2
        
        -attachForm $ppnColumn "top" 2   
         -attachForm $ppnColumn "left"   2
         -attachNone $ppnColumn "bottom"
         -attachForm $ppnColumn "right" 2 
        
     $ppnForm;
     
        string $vizColn = `columnLayout -adjustableColumn true pxpnVizColn`;
        setParent ..;
        
        string $brushColn = `columnLayout -adjustableColumn true pxpnBrushColn`;
        setParent ..;       
        
        string $groundColn = `columnLayout -adjustableColumn true pxpnGroundColn`;
        setParent ..; 
        
        string $createColn = `columnLayout -adjustableColumn true pxpnCreateColn`;
        setParent ..;

        string $edtColn = `columnLayout -adjustableColumn true pxpnEditColn`;
        setParent ..; 
	
        string $injectColn = `columnLayout -adjustableColumn true pxpnInjectColn`;                    
        setParent ..;  
        
    tabLayout -edit
        -tabLabel $vizColn "Viz"
        -tabLabel $brushColn "Brush"
        -tabLabel $groundColn "Ground"
        -tabLabel $createColn "Create"
        -tabLabel $edtColn "Edit"
         -tabLabel $injectColn "Misc" 
         $ppnTab;
         
	setUITemplate -popTemplate;

/// setup tab content	
	proxyPaintCreateVizUI($vizColn); 
    proxyPaintCreateBrushUI($brushColn); 
    proxyPaintGroundEditUI($groundColn);
    proxyPaintCreateCreateUI($createColn);
	proxyPaintCreateEditUI($edtColn);
	proxyPaintCreateInjectUI($injectColn);

	//proxyPaintSetCallbacks($parent);
	proxyPaintCreateExmpUI("pxpnExmpUIColn1cc22e1b");
	proxyPaintUpdateExampleScroll();
}

global proc proxyPaint_listAllVizToPaint()
{
    textScrollList -e -ra proxyPaint_VizToPaintScroll;
    string $vizs[] = `ls -l -type proxyViz`;
    string $aviz;
    for($aviz in $vizs) {
        textScrollList -e -append $aviz proxyPaint_VizToPaintScroll;
    }
}

global proc proxyPaint_selectVizToPaint()
{
    string $vizs[] = `textScrollList -q -selectItem proxyPaint_VizToPaintScroll`;
    if(size($vizs) < 1) {
        print "no proxyViz selected, do nothing\n";
        return;
    }
    
    select -r $vizs[0];
    proxyPaintUpdateExampleScroll;
    
    string $ani;
    for($ani in `textScrollList -q -allItems proxyPaint_VizToPaintScroll`) {
        if($ani == $vizs[0]) setAttr ($ani + ".act") 1;
        else setAttr ($ani + ".act") 0;
    }
    
    string $toolName = `currentCtx`;
    
    proxyPaintToolContext -e -slv $toolName;
    
    proxyPaint_updateVizStats($toolName);
    
}

global proc proxyPaintUpdateExampleScroll()
{
    proxyPaintToolContext -e -plantType 0 `currentCtx`;
    string $node = proxyPaint_getCurrentViz();
    if($node == "") {
        text -e -label "No Viz Selected" proxyPaint_vizNameText;
        return;
    }
    
    text -e -label $node proxyPaint_vizNameText;
    proxyPaintUpdateExmpUI("pxpnExmpUIColn1cc22e1b");
    
}
//:~
